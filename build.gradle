plugins {
    id 'com.jfrog.bintray' version '1.8.4' apply false
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

    group 'uk.gov.hmcts.reform'
    version buildNumber

    compileJava   {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }

    checkstyle {
        maxWarnings = 0
        toolVersion '8.29'
        sourceSets = [project.sourceSets.main]
    }

    jacocoTestReport {
        reports {
            html.enabled true
        }
    }
}

def libraries = [project(":ccd-sdk-types"), project(":ccd-config-generator")]

configure(libraries) {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
    }

    def gitRepo = "https://github.com/hmcts/ccd-config-generator"

    def pomConfig = {
        licenses {
            license {
                name "MIT License"
                url "http://www.opensource.org/licenses/mit-license.php"
                distribution "repo"
            }
        }

        scm {
            url "https://github.com/hmcts/ccd-config-generator"
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            Main(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                version project.version

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Generate CCD Configuration from code')
                    root.appendNode('name', 'CCD Config generator')
                    root.appendNode('url', gitRepo)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['Main']
        publish = true
        pkg {
            repo = 'hmcts-maven'
            name = project.name
            userOrg = 'hmcts'
            licenses = ['MIT']
            vcsUrl = gitRepo
            version {
                name = project.version
            }
        }
    }
}
